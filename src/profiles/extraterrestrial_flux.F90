! Copyright (C) 2020 National Center for Atmospheric Research
! SPDX-License-Identifier: Apache-2.0
!
!> Extraterrestrial flux profile type
module tuvx_profile_extraterrestrial_flux

  use musica_constants,  only : &
    dk => musica_dk, ik => musica_ik, lk => musica_lk
  use tuvx_Profile,      only : profile_t

  implicit none

  public :: extraterrestrial_flux_t

  type, extends(profile_t) :: extraterrestrial_flux_t
  contains
    final     :: finalize
  end type extraterrestrial_flux_t

  !> Constructor
  interface extraterrestrial_flux_t
    module procedure constructor
  end interface extraterrestrial_flux_t

contains

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  !> Initialize grid
  function constructor( profile_config, grid_warehouse ) result ( this )

    use musica_config, only : config_t
    use musica_string, only : string_t
    use musica_assert, only : die_msg
    use tuvx_util,     only : addpnt
    use tuvx_grid,     only : grid_t
    use tuvx_constants,       only : hc, deltax
    use tuvx_grid_warehouse,  only : grid_warehouse_t
    use tuvx_diagnostic_util, only : diagout
    use tuvx_interpolate

    !> arguments
    type(extraterrestrial_flux_t), pointer :: this
    type(config_t),           intent(inout) :: profile_config
    type(grid_warehouse_t),   intent(inout) :: grid_warehouse

    !> Local variables
    character(len=*), parameter :: Iam = &
      'extraterrestrial flux Profile initialize: '

    integer(ik), parameter :: Ok = 0_ik
    integer(ik), parameter :: inUnit = 20_ik
    real(dk),    parameter :: bin_edge(0:4) = (/ &
      0.0_dk,150.01_dk,200.07_dk,1000.99_dk,real(huge(0.0_dk),dk) &
    /)
    character(len=*), parameter :: comment = '#!$%*'
    class(grid_t), pointer :: lambdaGrid

    integer(ik) :: istat
    integer(ik) :: fileNdx, nFiles, ndx, nBins, nLines, Line
    real(dk)    :: zd, Value
    real(dk), allocatable :: inputGrid(:), inputData(:), tmpinputGrid(:)
    real(dk), allocatable :: interpolatedEtfl(:)
    logical(lk) :: found
    character(len=132)                 :: InputLine, trimInputLine
    character(len=512)                 :: IoMsg
    type(string_t)                     :: Handle
    type(string_t)                     :: defaultInterpolator
    type(string_t), allocatable        :: Filespec(:), Interpolator(:)
    class(abs_interpolator_t), pointer :: theInterpolator

    allocate( this )

    defaultInterpolator = 'interp2'

    ! Get the configuration settings
    call profile_config%get( 'file path', Filespec, Iam )
    call profile_config%get( 'name', this%handle_, Iam, default = 'None' )
    call profile_config%get( 'interpolator', Interpolator, Iam, found=found )
    nFiles = size(Filespec)
    if( .not. found ) then
      allocate( Interpolator(nFiles) )
      Interpolator = defaultInterpolator
    endif

    Handle = 'wavelength'
    lambdaGrid => grid_warehouse%get_grid( Handle )
    nBins = lambdaGrid%ncells_

    file_loop: &
    do fileNdx = 1,nFiles
      if(Interpolator(fileNdx) == "") then
        Interpolator(fileNdx) = defaultInterpolator
      endif

      ! Does input grid file exist?
      inquire( file=Filespec(fileNdx)%to_char(), exist=found )
      if( .not. found ) then
        call die_msg( 560768215, "File " &
          // Filespec(fileNdx)%to_char() // " not found" )
      endif

      open(unit=inUnit, file=Filespec(fileNdx)%to_char(), &
        iostat=istat,iomsg=IoMsg)

      if( istat /= Ok ) then
        call die_msg( 560768231, "Error opening " // &
          Filespec(fileNdx)%to_char() )
      endif

      ! Determine number of lines in file
      nLines = 0
      do
        read(inUnit,'(a)',iostat=istat) InputLine
        if( istat == Ok ) then
          trimInputLine = adjustl(InputLine)
          if( verify( trimInputLine(1:1),comment ) /= 0 ) then
            nLines = nLines + 1
            cycle
          endif
        else
          rewind(unit=inUnit)
          exit
        endif
      enddo

      ! Skip the header
      do
        read(inUnit,'(a)',iostat=istat) InputLine
        if( istat /= Ok ) then
          call die_msg( 560768227, "Error reading " // &
            Filespec(fileNdx)%to_char() )
        else
          trimInputLine = adjustl(InputLine)
          if( verify( trimInputLine(1:1),comment ) /= 0 ) then
            exit
          endif
        endif
      enddo

      allocate( inputGrid(nLines) )
      allocate( inputData(nLines) )
      !> Read the data
      Line = 1
      do
        trimInputLine = adjustl(InputLine)
        if( verify( trimInputLine(1:1),comment ) /= 0 ) then
          read(InputLine,*,iostat=istat,iomsg=IoMsg) &
            inputGrid(Line),inputData(Line)

          if( istat /= Ok ) then
            call die_msg( 560768229, "Invalid data format in " // &
              Filespec(fileNdx)%to_char() )
          endif
          Line = Line + 1
        endif
        read(inUnit,'(a)',iostat=istat) InputLine
        if( istat /= Ok ) then
          exit
        endif
      enddo

      close(unit=inUnit)

      ! special handling for neckel.flx
      if( index(Filespec(fileNdx)%to_char(),'neckel.flx') /= 0 ) then
        allocate( tmpinputGrid,source=inputGrid )
        where( inputGrid < 630._dk )
          tmpinputGrid = inputGrid - 0.5_dk
        elsewhere( inputGrid >= 630._dk .and. inputGrid < 870._dk )
          tmpinputGrid = inputGrid - 1.0_dk
        elsewhere( inputGrid >= 870._dk)
          tmpinputGrid = inputGrid - 2.5_dk
        endwhere
        inputData = 1.e13_dk*hc*inputData/inputGrid
        inputGrid = tmpinputGrid
        inputGrid = [inputGrid,inputGrid(size(inputGrid)) + 2.5_dk]
        inputData = [inputData,0.0_dk]
        deallocate( tmpinputGrid )
      else
        ! extend inputGrid,inputData to cover model photolysis grid
        call addpnt( x=inputGrid,y=inputData, &
          xnew=(1.0_dk-deltax)*inputGrid(1),ynew=0.0_dk )
        call addpnt( x=inputGrid,y=inputData, &
          xnew=0.0_dk,ynew=0.0_dk )
        call addpnt( x=inputGrid,y=inputData, &
          xnew=(1.0_dk+deltax)*inputGrid(size(inputGrid)),ynew=0.0_dk )
        call addpnt( x=inputGrid,y=inputData, &
          xnew=1.e38_dk,ynew=0.0_dk )
      endif
      ! assign interpolator for this dataset
      select case( Interpolator(fileNdx)%to_char() )
        case( 'interp1' )
          allocate( interp1_t :: theInterpolator )
        case( 'interp2' )
          allocate( interp2_t :: theInterpolator )
        case( 'interp3' )
          allocate( interp3_t :: theInterpolator )
        case( 'interp4' )
          allocate( interp4_t :: theInterpolator )
        case default
          call die_msg( 560768275, "interpolator " // &
            Interpolator(fileNdx)%to_char() // " not a valid selection" )
      end select

      ! interpolate from source to model wavelength grid
      interpolatedEtfl = theInterpolator%interpolate( &
        lambdaGrid%edge_, inputGrid, inputData, FoldIn=0 &
      )
      if( .not. allocated( this%mid_val_ ) ) then
        allocate( this%mid_val_,mold=interpolatedEtfl )
        this%mid_val_ = 0.0_dk
      endif

      ! assign interpolated source to model etfl
      where( &
        bin_edge(fileNdx-1) <= lambdaGrid%edge_(:nBins) .and. &
        lambdaGrid%edge_(:nBins) < bin_edge(fileNdx) &
      )
         this%mid_val_ = interpolatedEtfl
      endwhere

      ! test diagnostics
      if( index(Filespec(fileNdx)%to_char(),'susim') /= 0 ) then
        call diagout( 'susim.inputGrid.new', inputGrid )
        call diagout( 'susim.inputData.new', inputData )
        call diagout( 'susim.interpolated.new', interpolatedEtfl )
        call diagout( 'susim.etfl.new', this%mid_val_ )
      elseif( index(Filespec(fileNdx)%to_char(),'atlas') /= 0 ) then
        call diagout( 'atlas.inputGrid.new', inputGrid )
        call diagout( 'atlas.inputData.new', inputData )
        call diagout( 'atlas.interpolated.new', interpolatedEtfl )
        call diagout( 'atlas.etfl.new', this%mid_val_ )
      elseif( index(Filespec(fileNdx)%to_char(),'neckel') /= 0 ) then
        call diagout( 'neckel.inputGrid.new', inputGrid )
        call diagout( 'neckel.inputData.new', inputData )
        call diagout( 'neckel.interpolated.new', interpolatedEtfl )
        call diagout( 'neckel.etfl.new', this%mid_val_ )
      elseif( index(Filespec(fileNdx)%to_char(),'sao2010') /= 0 ) then
        call diagout( 'sao2010.inputGrid.new', inputGrid )
        call diagout( 'sao2010.inputData.new', inputData )
        call diagout( 'sao2010.interpolated.new', interpolatedEtfl )
        call diagout( 'sao2010.etfl.new', this%mid_val_ )
      endif

      deallocate( inputGrid,inputData )
      deallocate( theInterpolator )

    enddo file_loop

    deallocate( lambdaGrid )

    ! test diagnostics
    call diagout( 'etfl.new', this%mid_val_ )

  end function constructor

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  subroutine finalize( this )

    type(extraterrestrial_flux_t), intent(inout) :: this

    if( allocated( this%edge_val_ ) ) deallocate( this%edge_val_ )
    if( allocated( this%mid_val_ ) )  deallocate( this%mid_val_ )
    if( allocated( this%delta_val_ ) ) deallocate( this%delta_val_ )
    if( allocated( this%layer_dens_ ) ) deallocate( this%layer_dens_ )

  end subroutine finalize

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

end module tuvx_profile_extraterrestrial_flux
