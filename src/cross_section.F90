! Copyright (C) 2020 National Center for Atmospheric Research
! SPDX-License-Identifier: Apache-2.0

module tuvx_cross_section
! The base cross section type and related functions

  use musica_constants,                only : dk => musica_dk

  implicit none

  private
  public :: cross_section_t, cross_section_parms_t, base_constructor,         &
            cross_section_ptr

  type cross_section_parms_t
    ! local working type for holding cross section parameters
    real(dk), allocatable :: temperature(:) ! Temperature grid [K]
    real(dk), allocatable :: deltaT(:)      ! Temperature difference between grid sections [K]
    real(dk), allocatable :: array(:,:)     ! Cross section parameters (wavelength, parameter type)
  end type cross_section_parms_t

  type cross_section_t
    ! Calculator for cross_section
    ! The cross section array

    ! Cross section parameter sets
    type(cross_section_parms_t), allocatable :: cross_section_parms(:)
  contains
    !> Calculate the cross section
    procedure :: calculate => run
    !> Add points to the cross section grid based on configuration data
    procedure :: add_points
    final     :: finalize
  end type cross_section_t

  interface cross_section_t
    module procedure :: constructor
  end interface

  type cross_section_ptr
    ! Pointer type for building sets of photo rate constants
    class(cross_section_t), pointer :: val_ => null( )
  end type cross_section_ptr

  real(dk), parameter    :: rZERO = 0.0_dk
  real(dk), parameter    :: rONE  = 1.0_dk

contains

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  function constructor( config, grid_warehouse, profile_warehouse )           &
      result( new_obj )
    ! Create an instance of the base cross section type

    use musica_assert,                 only : assert_msg
    use musica_config,                 only : config_t
    use musica_string,                 only : string_t
    use tuvx_grid_warehouse,           only : grid_warehouse_t
    use tuvx_profile_warehouse,        only : profile_warehouse_t

    class(cross_section_t),    pointer       :: new_obj ! Base :f:type:`~tuvx_cross_section/cross_section_t` type
    type(config_t),            intent(inout) :: config ! Cross section configuration object
    type(grid_warehouse_t),    intent(inout) :: grid_warehouse ! A :f:type:`~tuvx_grid_warehouse/grid_warehouse_t`
    type(profile_warehouse_t), intent(inout) :: profile_warehouse ! A :f:type:`~tuvx_profile_warehouse/profile_warehouse_t`

    type(string_t) :: required_keys(1), optional_keys(4)

    required_keys(1) = "type"
    optional_keys(1) = "netcdf files"
    optional_keys(2) = "lower extrapolation"
    optional_keys(3) = "upper extrapolation"
    optional_keys(4) = "name"
    call assert_msg( 124969900,                                               &
                     config%validate( required_keys, optional_keys ),         &
                     "Bad configuration data format for "//                   &
                     "base cross section." )
    allocate( new_obj )
    call base_constructor( new_obj, config, grid_warehouse, profile_warehouse )

  end function constructor

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  subroutine base_constructor( new_obj, config, grid_warehouse,               &
      profile_warehouse )
    ! Initialize cross_section_t objects

    use musica_assert,                 only : die_msg
    use musica_config,                 only : config_t
    use musica_string,                 only : string_t
    use tuvx_grid_warehouse,           only : grid_warehouse_t
    use tuvx_grid,                     only : grid_t
    use tuvx_interpolate,              only : interpolator_conserving_t
    use tuvx_netcdf,                   only : netcdf_t
    use tuvx_profile_warehouse,        only : profile_warehouse_t
    use tuvx_profile,                  only : profile_t

    class(cross_section_t),    pointer       :: new_obj ! A :f:type:`~tuvx_cross_section/cross_section_t`
    type(config_t),            intent(inout) :: config ! Cross section configuration object
    type(grid_warehouse_t),    intent(inout) :: grid_warehouse ! A :f:type:`~tuvx_grid_warehouse/grid_warehouse_t`
    type(profile_warehouse_t), intent(inout) :: profile_warehouse ! A :f:type:`~tuvx_profile_warehouse/profile_warehouse_t`

    !   local variables
    character(len=*), parameter   :: Iam = 'base cross section initialize'
    character(len=*), parameter   :: Hdr = 'cross_section_'

    integer :: parmNdx, fileNdx
    integer :: nParms
    real(dk), allocatable :: data_lambda(:)
    real(dk), allocatable :: data_parameter(:)
    logical :: found
    character(len=:), allocatable :: msg
    type(netcdf_t), allocatable :: netcdf_obj
    type(string_t), allocatable :: netcdfFiles(:)
    class(grid_t), pointer :: lambdaGrid
    type(interpolator_conserving_t) :: interpolator

    !> Get model wavelength grids
    lambdaGrid => grid_warehouse%get_grid( "wavelength", "nm" )

    !> get cross section netcdf filespec
    call config%get( 'netcdf files', netcdfFiles, Iam, found = found )

has_netcdf_file: &
    if( found ) then
      allocate( new_obj%cross_section_parms( size( netcdfFiles ) ) )
file_loop: &
      do fileNdx = 1, size( new_obj%cross_section_parms )
        allocate( netcdf_obj )
        ! read netcdf cross section parameters
        call netcdf_obj%read_netcdf_file(                                     &
                          file_path = netcdfFiles( fileNdx )%to_char( ),      &
                          variable_name = Hdr )
        nParms = size( netcdf_obj%parameters, dim = 2 )
        if( nParms < 1 ) then
          msg = Iam//'File: '//trim( netcdfFiles( fileNdx )%to_char( ) )//    &
                       '  parameters array has < 1 column'
          call die_msg( 520647236, msg )
        endif

        ! interpolate from data to model wavelength grid
        if( allocated( netcdf_obj%wavelength ) ) then
          if( .not. allocated( new_obj%cross_section_parms( fileNdx )%array) )&
              then
            allocate( new_obj%cross_section_parms( fileNdx )%array(           &
                                                lambdaGrid%ncells_, nParms ) )
          endif
          do parmNdx = 1, nParms
            data_lambda    = netcdf_obj%wavelength
            data_parameter = netcdf_obj%parameters( :, parmNdx )
            call new_obj%add_points( config, data_lambda, data_parameter )
            new_obj%cross_section_parms( fileNdx )%array( :, parmNdx ) =      &
                interpolator%interpolate( x_target = lambdaGrid%edge_,        &
                                          x_source = data_lambda,             &
                                          y_source = data_parameter )
          enddo
        else
          new_obj%cross_section_parms( fileNdx )%array = netcdf_obj%parameters
        endif
        if( allocated( netcdf_obj%temperature ) ) then
          new_obj%cross_section_parms( fileNdx )%temperature =                &
              netcdf_obj%temperature
        endif
        deallocate( netcdf_obj )
      enddo file_loop
    endif has_netcdf_file

    deallocate( lambdaGrid )

  end subroutine base_constructor

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  function run( this, grid_warehouse, profile_warehouse, at_mid_point )       &
      result( cross_section )
    ! Calculate the cross section for a given set of environmental conditions

    use musica_string,                 only : string_t
    use tuvx_grid,                     only : grid_t
    use tuvx_grid_warehouse,           only : grid_warehouse_t
    use tuvx_profile_warehouse,        only : profile_warehouse_t

    real(kind=dk), allocatable               :: cross_section(:,:) ! Calculated cross section
    class(cross_section_t),    intent(in)    :: this ! A :f:type:`~tuvx_cross_section/cross_section_t`
    type(grid_warehouse_t),    intent(inout) :: grid_warehouse ! A :f:type:`~tuvx_grid_warehouse/grid_warehouse_t`
    type(profile_warehouse_t), intent(inout) :: profile_warehouse ! A :f:type:`~tuvx_profile_warehouse/profile_warehouse_t`
    logical, optional,         intent(in)    :: at_mid_point ! Flag indicating that the cross section data is at grid mid-points. If omitted or false, data is assumed to be at interfaces

    !> Local variables
    integer :: colndx
    integer :: nzdim
    character(len=*), parameter :: Iam =                                      &
        'radXfer base cross section calculate: '
    class(grid_t), pointer     :: zGrid => null( )
    real(dk),      allocatable :: wrkCrossSection(:,:)

    zGrid => grid_warehouse%get_grid( "height", "km" )

    nzdim = zGrid%ncells_ + 1
    if( present( at_mid_point ) ) then
      if( at_mid_point ) then
        nzdim = nzdim - 1
      endif
    endif

    allocate( wrkCrossSection( size( this%cross_section_parms(1)%array,       &
                                                          dim = 1 ), nzdim ) )

    !> Just copy the lambda interpolated array
    do colndx = 1, nzdim
      wrkCrossSection( :, colndx ) = this%cross_section_parms(1)%array(:,1)
    enddo

    cross_section = transpose( wrkCrossSection )

    deallocate( zGrid )

  end function run

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  subroutine add_points( this, config, data_lambda, data_parameter )
    ! Adds points to the cross section grid based on configuration data

    use musica_assert,                 only : assert_msg, die_msg
    use musica_config,                 only : config_t
    use musica_string,                 only : string_t
    use tuvx_util,                     only : add_point

    class(cross_section_t), intent(in)    :: this ! This :f:type:`~tuvx_cross_section/cross_section_t`
    type(config_t),         intent(inout) :: config ! The configuration used to build this object
    real(dk), allocatable,  intent(inout) :: data_lambda(:) ! Wavelength grid
    real(dk), allocatable,  intent(inout) :: data_parameter(:) ! Parameters (wavelength)

    real(dk), parameter :: deltax = 1.e-5_dk
    character(len=*), parameter :: Iam = 'cross_section; addpnts: '

    integer  :: nRows
    real(dk) :: lowerLambda, upperLambda
    real(dk) :: addpnt_val_lower, addpnt_val_upper
    type(string_t)  :: addpnt_type
    type(config_t)  :: extrap_config
    logical         :: found
    character(len=:), allocatable :: number
    type(string_t) :: required_keys(1), optional_keys(1)

    required_keys(1) = "type"
    optional_keys(1) = "value"

    nRows = size( data_lambda )
    lowerLambda = data_lambda(1) ; upperLambda = data_lambda( nRows )

    ! add endpoints to data arrays; first the lower bound
    addpnt_val_lower = rZERO
    call config%get( 'lower extrapolation', extrap_config, Iam, found = found )
    if( found ) then
      call assert_msg( 671608110,                                             &
                       extrap_config%validate( required_keys, optional_keys ),&
                       "Bad format for extrapolation in base cross section." )
      call extrap_config%get( "type", addpnt_type, Iam )
      if( addpnt_type == "boundary" ) then
        addpnt_val_lower = data_parameter(1)
      elseif( addpnt_type == "constant" ) then
        call extrap_config%get( "value", addpnt_val_lower, Iam )
      else
        call die_msg( 316405971,                                              &
                      "Bad extrapolation type: '"//addpnt_type//"'" )
      endif
    endif

    !> add endpoints to data arrays; now the upper bound
    addpnt_val_upper = rZERO
    call config%get( 'upper extrapolation', extrap_config, Iam, found = found )
    if( found ) then
      call assert_msg( 918590095,                                             &
                       extrap_config%validate( required_keys, optional_keys ),&
                       "Bad format for extrapolation in base cross section." )
      call extrap_config%get( "type", addpnt_type, Iam )
      if( addpnt_type == "boundary" ) then
        addpnt_val_upper = data_parameter( nRows )
      elseif( addpnt_type == "constant" ) then
        call extrap_config%get( "value", addpnt_val_upper, Iam )
      else
        call die_msg( 302970879,                                              &
                      "Bad extrapolation type: '"//addpnt_type//"'" )
      endif
    endif

    call add_point( x = data_lambda, y = data_parameter,                      &
                    xnew = ( rONE - deltax ) * lowerLambda,                   &
                    ynew = addpnt_val_lower )
    call add_point( x = data_lambda, y = data_parameter, xnew = rZERO,        &
                    ynew = addpnt_val_lower )
    call add_point( x = data_lambda, y = data_parameter,                      &
                    xnew = ( rONE + deltax ) * upperLambda,                   &
                    ynew = addpnt_val_upper )
    call add_point( x = data_lambda, y = data_parameter, xnew = 1.e38_dk,     &
                    ynew = addpnt_val_upper )

  end subroutine add_points

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  subroutine finalize( this )
    ! finalize the cross section type

    type(cross_section_t), intent(inout) :: this ! This :f:type:`~tuvx_cross_section/cross_section_t`

    integer :: ndx

    if( allocated( this%cross_section_parms ) ) then
      do ndx = 1,size( this%cross_section_parms )
        if( allocated( this%cross_section_parms( ndx )%array ) ) then
          deallocate( this%cross_section_parms( ndx )%array )
        endif
        if( allocated( this%cross_section_parms( ndx )%temperature ) ) then
          deallocate( this%cross_section_parms( ndx )%temperature )
        endif
      enddo
      deallocate( this%cross_section_parms )
    endif

  end subroutine finalize

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

end module tuvx_cross_section
